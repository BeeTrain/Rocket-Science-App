import ru.chernakov.rocketscienceapp.AndroidConfig
import ru.chernakov.rocketscienceapp.Modules
import ru.chernakov.rocketscienceapp.Plugins

apply plugin: Plugins.application
apply plugin: Plugins.firebaseCrashlytics
apply plugin: Plugins.firebaseAppDistribution

apply from: '../config/config.gradle'
apply from: '../config/dependencies.gradle'

android {
    defaultConfig {
        applicationId AndroidConfig.ID

        versionCode rootProject.versionCode
        versionName rootProject.versionName
    }

    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        releaseQa {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                appId = System.getenv(AndroidConfig.GOOGLE_APP_ID)
                groups = AndroidConfig.GROUP_QA
                releaseNotesFile = file(AndroidConfig.VERSIONS_FILE_PATH).absolutePath
            }
        }

        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
    }
}

dependencies {
    implementation project(Modules.APP_CORE)

    implementation project(Modules.FEATURE_SPLASH)
    implementation project(Modules.FEATURE_AUTH)
    implementation project(Modules.FEATURE_FAVORITE)
    implementation project(Modules.FEATURE_PROFILE)
    implementation project(Modules.FEATURE_APPFEATURES)

    implementation project(Modules.FEATURE_APP_BUBBLEGAME)
    implementation project(Modules.FEATURE_APP_MOVIES)
    implementation project(Modules.FEATURE_APP_APPMONITOR)
    implementation project(Modules.FEATURE_APP_PAINT)

    multidex()
    kotlinLib()
    androidxCore()
    supportCore()
    viewModel()
    navigation()
    network()
    firebaseAuth()
    firebaseQuality()
    playServicesAuth()
    koin()
    timber()
    tests()
}

buildtimetracker {
    reporters {
        csv {
            output "build/times.csv"
            append true
            header false
        }

        summary {
            ordered true
            threshold 50
            barstyle "none"
        }

        csvSummary {
            csv "build/times.csv"
        }
    }
}

apply plugin: Plugins.googleServices