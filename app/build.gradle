import ru.chernakov.AndroidConfig
import ru.chernakov.Modules
import ru.chernakov.Plugins

apply plugin: Plugins.application

apply from: '../config/config.gradle'
apply from: '../config/dependencies.gradle'

apply from: '../config/quality/checkstyle.gradle'
apply from: '../config/quality/ktlint.gradle'
apply from: '../config/quality/jacoco.gradle'

android {
    defaultConfig {
        applicationId AndroidConfig.ID
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
        }
    }
}

dependencies {
    implementation project(Modules.CORE_BASE)
    implementation project(Modules.CORE_UI)

    implementation project(Modules.FEATURE_SPLASH)
    implementation project(Modules.FEATURE_LOGIN)
    implementation project(Modules.FEATURE_REGISTER)
    implementation project(Modules.FEATURE_FLOW)
    implementation project(Modules.FEATURE_PROFILE)
    implementation project(Modules.FEATURE_SETTINGS)
    implementation project(Modules.FEATURE_APPFEATURES)

    multidex()

    kotlinLib()

    androidxCore()
    supportCore()

    viewModel()

    network()

    firebase()
    playServicesAuth()

    koin()

    picasso()

    timber()

    leakCanary()

    tests()
}

buildtimetracker {
    reporters {
        csv {
            output "build/times.csv"
            append true
            header false
        }

        summary {
            ordered true
            threshold 50
            barstyle "none"
        }

        csvSummary {
            csv "build/times.csv"
        }
    }
}

task checkBeforePush(group: 'verification',
        description: 'Inspect your code before push') {
    dependsOn 'checkstyle', 'detekt', 'ktlint', 'lintDebug'
}
apply plugin: Plugins.googleServices
